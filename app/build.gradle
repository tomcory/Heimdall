plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.google.devtools.ksp"
    id "androidx.navigation.safeargs"
    id "com.google.protobuf" version "0.9.0"
    id "org.jetbrains.kotlin.plugin.serialization" version "1.8.21"
}

kotlin {
    jvmToolchain(11)
}

android {
    namespace "de.tomcory.heimdall"

    defaultConfig {
        applicationId "de.tomcory.heimdall"
        minSdk 28
        compileSdk 33
        targetSdk 33
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def secureProps = new Properties()
        vectorDrawables {
            useSupportLibrary true
        }
        if (file("../secure.properties").exists()) {
            file("../secure.properties")?.withInputStream { secureProps.load(it) }
        }
        //resValue "string", "maps_api_key", (secureProps.getProperty("EXAMPLE") ?: "")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    // Enables data binding.
    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding true
        viewBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

    // netty stuff below
    ndkVersion = "21.0.6113669"

    packagingOptions {
        resources {
            excludes += ["META-INF/INDEX.LIST", "META-INF/io.netty.versions.properties"]
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.4.4"
    }

    configurations.configureEach {
        resolutionStrategy {
            force "io.netty:netty-all:4.1.58.Final"
        }
    }

    sourceSets {
        main {
            proto {
                srcDir "src/main/proto/proto" // default value
            }
        }

        main.java.srcDirs += "src/main/proto"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.12"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

ext {
    bouncycastle_version = "1.69"
    room_version = "2.5.1"
    lifecycle_version = "2.5.1"
    test_version = "1.5.0"
    data_binding_compiler_version = gradle_version // Always need to be the same.
    compose_version = "1.4.3"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // AndroidX compatibility library
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.palette:palette-ktx:1.0.0"

    // AndroidX Lifecycle Components
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Jetpack Compose
    implementation "androidx.activity:activity-compose:1.7.1"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material3:material3:1.2.0-alpha01"
    implementation "androidx.compose.material3:material3-window-size-class:1.2.0-alpha01"
    implementation "com.google.accompanist:accompanist-drawablepainter:0.29.0-alpha"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.29.0-alpha"

    // Jetpack DataStore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "com.google.protobuf:protobuf-javalite:3.21.12"

    // Kotlin serialisation
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0"

    // Provides the utilities needed to use Vico in Jetpack Compose.
    implementation "com.patrykandpatrick.vico:compose:1.6.2"
    // An optional addition for `vico.compose` that creates a `ChartStyle` based on an M3 Material Theme.
    implementation "com.patrykandpatrick.vico:compose-m3:1.6.2"

    // Room dependencies
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-compose:$navigation_version"

    // OpenCSV library
    implementation "com.opencsv:opencsv:5.6"

    // pcap4j
    implementation "org.pcap4j:pcap4j-core:1.7.6"
    implementation "org.pcap4j:pcap4j-packetfactory-static:1.7.6"

    // Dex analyser used to detect tracker libraries in apps
    implementation "com.github.lanchon.dexpatcher:multidexlib2:2.3.4.r2"
    implementation "net.dongliu:apk-parser:2.6.10"

    // various UI libraries
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    // Timber
    implementation "com.jakewharton.timber:timber:4.7.1"

    // BouncyCastle and Littleshoot
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncycastle_version"
    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastle_version"
    implementation ("org.littleshoot:dnssec4j:0.1.6") { exclude group: "org.slf4j" }
    implementation "commons-io:commons-io:2.6"
    implementation group: "io.netty", name: "netty-all", version: "4.1.58.Final"
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation ("net.lightbody.bmp:mitm:2.1.5") { exclude group: "org.slf4j"}
    implementation group: "com.jcraft", name: "jzlib", version: "1.1.3"
    implementation "commons-io:commons-io:2.6"
    implementation "com.google.guava:guava:24.1-jre"

    // test stuff
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test:core:$test_version"
    androidTestImplementation "androidx.test:runner:$test_version"
    androidTestImplementation "androidx.test:rules:$test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    testImplementation "org.robolectric:robolectric:4.8"
}